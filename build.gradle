buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.1.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "com.github.hierynomus.license" version "0.15.0"
    id "com.diffplug.gradle.spotless" version "3.23.1"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
}

apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'

repositories {
    mavenCentral()
    jcenter()
}


group 'net.kebernet'
version '0.1.1'


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

spotless {
    groovy {
        excludeJava()
        paddedCell()
    }
    java {
        googleJavaFormat() // use a specific formatter for Java files
    }
    kotlin {
        ktlint().userData(['indent_size': '4', 'continuation_indent_size': '4'])
    }

}

dependencyCheck {
    suppressionFile = project.file("src/etc/suppression.xml")
    format = 'ALL'
}


sourceSets {
    functionalTest {
        groovy.srcDir file('src/functional/groovy')
        java.srcDir file('src/functional/java')
        resources.srcDir file('src/functional/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test //, integrationTest
    testLogging {
        showStandardStreams = true
    }
    jacoco {
        enabled = true
        append = true
        destinationFile = file("${buildDir}/jacoco/test.exec")
    }
}


jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

check.dependsOn functionalTest
check.dependsOn dependencyCheckAnalyze
check.dependsOn licenseFormat
check.finalizedBy jacocoTestReport


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

license {
    header rootProject.file('src/etc/HEADER')
    strictCheck true
    mapping("java", "SLASHSTAR_STYLE")
}

pluginBundle {
    website = "https://github.com/atl-tw/world-engine"
    vcsUrl = "https://github.com/atl-tw/world-engine.git"
    description = "Opinionated Terraform"
    tags = ['terraform', 'aws', 'gcp', 'azure']

    plugins {
        worldEngine {
            id = 'net.kebernet.world-engine'
            displayName = "World Engine"
            description = "Opinionated Terraform"
        }
    }
}

if(project.hasProperty("kebernet_bintray")) {
    bintray {
        user = getProperty("kebernet_bintray")
        key = getProperty("kebernet_bintray_api")
        pkg {
            repo = 'maven'
            name = 'net.kebernet.worldengine'
            publish = true
            licenses = ['Apache-2.0']
            publications = ['worldEngine']
            version {
                attributes = [
                        'gradle-plugin': "net.kebernet.world-engine:net.kebernet.world-engine.gradle.plugin:${project.version}"
                ]
            }
        }
    }
}

publishing {
    publications {
        worldEngine(MavenPublication) {
            from components.java
            artifact source: "${project.buildDir}/libs/${project.name}-${project.version}-javadoc.jar", extension: 'jar', classifier: 'javadoc'
            artifact source: "${project.buildDir}/libs/${project.name}-${project.version}-sources.jar", extension: 'jar', classifier: 'sources'
            groupId "${project.group}.${project.name}"
            artifactId "${project.group}.${project.name}.gradle.plugin"
            version project.version
        }
    }
}

gradlePlugin {
    plugins {
        worldEngine {
            id = 'net.kebernet.world-engine'
            implementationClass = 'net.kebernet.worldengine.WorldEngine'
        }
    }
}
